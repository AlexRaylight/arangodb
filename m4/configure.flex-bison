dnl -*- mode: Autoconf; -*-

dnl -----------------------------------------------------------------------------------------
dnl check for flex
dnl -----------------------------------------------------------------------------------------

AC_PROG_LEX

tr_flex_usable=no

if test "x$LEX" != x -a "x$LEX" != "x:";  then
  AC_MSG_CHECKING([if flex supports required features])

  [tr_flex_version=`$LEX --version | sed -e 's:^[^0-9]*::' | awk -F. '{print $1 * 10000 + $2 * 100 + $3}'`]

  if test "$tr_flex_version" -ge 20535;  then
    tr_flex_usable=yes
  fi

  AC_MSG_RESULT($tr_flex_usable)
fi

if test "x$tr_flex_usable" != "xyes"; then
  AC_MSG_WARN([Please install flex version 2.5.35 or higher from http://ftp.gnu.org/gnu/flex/])
fi

AM_CONDITIONAL(ENABLE_FLEX, test "x$tr_flex_usable" = xyes)

dnl -----------------------------------------------------------------------------------------
dnl check for bison
dnl -----------------------------------------------------------------------------------------

AC_CHECK_PROG(BISON, bison, bison)

tr_bison_usable=no

if test "x$BISON" != x -a "x$BISON" != "x:"; then
  AC_MSG_CHECKING([if bison supports required features])

  [tr_bison_version=`$BISON --version | head -1 | sed -e 's:^[^0-9]*::' | awk -F. '{print $1 * 10000 + $2 * 100 + $3}'`] 

  if test "$tr_bison_version" -ge 20401; then
    tr_bison_usable=yes
  fi

  AC_MSG_RESULT($tr_bison_usable)
fi

if test "x$tr_bison_usable" != "xyes"; then
  AC_MSG_WARN([Please install bison version 2.4.1 or higher from http://ftp.gnu.org/gnu/bison/])
fi

AM_CONDITIONAL(ENABLE_BISON, test "x$tr_bison_usable" = xyes)

dnl -----------------------------------------------------------------------------------------
dnl informational output
dnl -----------------------------------------------------------------------------------------

BASIC_INFO="$BASIC_INFO|ENABLE_FLEX:  ${tr_flex_usable}"
BASIC_INFO="$BASIC_INFO|ENABLE_BISON: ${tr_bison_usable}"
