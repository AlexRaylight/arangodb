dnl -*- mode: Autoconf; -*-

dnl ----------------------------------------------------------------------------
dnl --SECTION--                                                          NCURSES
dnl ----------------------------------------------------------------------------

AC_MSG_NOTICE([--------------------------------------------------------------------------------])
AC_MSG_NOTICE([CHECKING FOR NCURSES])
AC_MSG_NOTICE([--------------------------------------------------------------------------------])

TR_STATIC_ENABLE([ncurses])

dnl ----------------------------------------------------------------------------
dnl checks for ncurses headers and libraries
dnl ----------------------------------------------------------------------------

AC_LANG(C)

tr_NCURSES="yes"

AC_PATH_PROG(NCURSES_CONFIG, ncurses5-config, [], [$PATH:/usr/sbin/])

if test -z "$NCURSES_CONFIG"; then
  AC_PATH_PROG(NCURSES_CONFIG, ncurses6-config, [], [$PATH:/usr/sbin/])
fi

if test -n "$NCURSES_CONFIG"; then
  NCURSES_LIBS="`$NCURSES_CONFIG --libs`"
  NCURSES_CPPFLAGS="`$NCURSES_CONFIG --cflags`"

  TR_ABSOLUTE_LIBRARIES([ncurses], [NCURSES_LIBS])

  if test "x$tr_libraries_found" != xyes;  then
    tr_NCURSES="no"
  fi

  TRI_NCURSES_VERSION="`$NCURSES_CONFIG --version`"
else
  TR_FIND_C_LIBRARY([ncurses], [ncurses], [NCURSES_LIBS], [setupterm])
  tr_NCURSES=$tr_library_found
  TRI_NCURSES_VERSION="ncurses"
fi

dnl ----------------------------------------------------------------------------
dnl check if ncurses headers are present
dnl ----------------------------------------------------------------------------
    
AC_CHECK_HEADERS(curses.h, [], [tr_NCURSES="no"])
AC_CHECK_HEADERS(term.h, [], [tr_NCURSES="no"])

if test "x$tr_NCURSES" != xyes;  then
  AC_MSG_ERROR([Please install the ncurses header files])
fi

AC_MSG_CHECKING([NCURSES version])
AC_MSG_RESULT([$TRI_NCURSES_VERSION])

NCURSES_CPPFLAGS="${NCURSES_CPPFLAGS} -DTRI_NCURSES_VERSION='\"${TRI_NCURSES_VERSION}\"'"

dnl ----------------------------------------------------------------------------
dnl fix include and static libraries
dnl ----------------------------------------------------------------------------

TR_INCLUDE([NCURSES_CPPFLAGS])

TR_STATIC_LIBRARY([ncurses], [NCURSES_LIBS])

dnl ----------------------------------------------------------------------------
dnl add substitutions
dnl ----------------------------------------------------------------------------

AC_SUBST(NCURSES_CPPFLAGS)
AC_SUBST(NCURSES_LDFLAGS)
AC_SUBST(NCURSES_LIBS)

dnl ----------------------------------------------------------------------------
dnl informational output
dnl ----------------------------------------------------------------------------

LIB_INFO="$LIB_INFO|NCURSES VERSION: ${TRI_NCURSES_VERSION}"

LIB_INFO="$LIB_INFO|NCURSES_CPPLIBS: ${NCURSES_CPPLIBS}"
LIB_INFO="$LIB_INFO|NCURSES_LDLIBS:  ${NCURSES_LDLIBS}"
LIB_INFO="$LIB_INFO|NCURSES_LIBS:     ${NCURSES_LIBS}"

LIB_INFO="$LIB_INFO|."

dnl ----------------------------------------------------------------------------
dnl --SECTION--                                                      END-OF-FILE
dnl ----------------------------------------------------------------------------

dnl Local Variables:
dnl mode: outline-minor
dnl outline-regexp: "^\\(dnl --SECTION--\\)"
dnl End:
